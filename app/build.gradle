plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

// Adiciona a logica para ler o arquivo local.properties
Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}

android {
    // Namespace e Application ID devem ser consistentes
    namespace 'com.example.goplan'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.goplan"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [ MAPS_API_KEY:"${properties.getProperty("MAPS_API_KEY", "")}" ]

        // Disponibiliza as chaves para o codigo Java/Kotlin (BuildConfig)
        buildConfigField "String", "MAPS_API_KEY", "\"${properties.getProperty("MAPS_API_KEY", "")}\""
        buildConfigField "String", "GOOGLE_WEB_CLIENT_ID", "\"${properties.getProperty("GOOGLE_WEB_CLIENT_ID", "")}\""

        // Disponibiliza o token do Mapbox como um recurso de string
        resValue "string", "mapbox_access_token", "\"${properties.getProperty("MAPBOX_ACCESS_TOKEN", "")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    // Habilita a geracao do BuildConfig
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.activity:activity:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Import the Firebase BoM (versao atualizada)
    implementation platform('com.google.firebase:firebase-bom:34.6.0')

    // Dependencias do Firebase
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'

    // Dependencia para Login com Google
    implementation 'com.google.android.gms:play-services-auth:21.2.0'

    // Dependencia para Servicos de Localizacao
    implementation 'com.google.android.gms:play-services-location:21.2.0'

    // Dependencia do Mapbox Maps SDK
    implementation 'com.mapbox.maps:android:11.4.0'

    // Dependencias para a API do Google Calendar
    implementation 'com.google.api-client:google-api-client-android:2.2.0'
    implementation 'com.google.apis:google-api-services-calendar:v3-rev20220715-2.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
